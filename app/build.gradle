plugins {
    id 'insulator.common'
    id 'insulator.jpackage'
    id 'insulator.javafx'
    id 'insulator.application'
    id "org.unbroken-dome.test-sets"
}

javafx {
    version = "15.0.1"
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.swing']
}

apply plugin: 'kotlin-kapt'

def dagger_version = "2.33"

testSets { integrationTest }

dependencies {
    implementation project(":lib:helper")
    implementation project(":lib:jsonhelper")
    implementation project(":lib:configuration")
    implementation project(":lib:update")
    implementation project(":lib:kafka")

    // Dagger
    implementation(group: 'com.google.dagger', name: 'dagger', version: "$dagger_version")
    kapt(group: 'com.google.dagger', name: 'dagger-compiler', version: "$dagger_version")

    // Integration tests
    testImplementation(group: 'org.testfx', name: 'openjfx-monocle', version: "jdk-12.0.1+2")
    integrationTestImplementation(group: 'org.testcontainers', name: 'kafka', version: "1.15.3")
    integrationTestImplementation(group: 'org.apache.kafka', name: 'kafka-clients', version: '6.0.0-ce')
}

compileIntegrationTestKotlin { kotlinOptions.jvmTarget = "1.8" }
integrationTest.outputs.upToDateWhen { false }

jar { manifest { attributes('Main-Class': 'insulator.AppKt') } }

application { mainClassName = 'insulator.AppKt' }

jacocoIntegrationTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

integrationTest{
    minHeapSize = "512m"
    maxHeapSize = "2G"
}