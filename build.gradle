plugins {
    id 'idea'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'com.adarshr.test-logger' version '2.1.0'
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "org.jlleitschuh.gradle.ktlint" version "9.4.0"
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
    id "org.unbroken-dome.test-sets" version "3.0.1"
    id 'jacoco'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
    maven { url "https://kotlin.bintray.com/kotlinx/" }
    maven { url "https://packages.confluent.io/maven/" }
    maven { url "https://repository.mulesoft.org/nexus/content/repositories/public/" }
}

javafx {
    version = "14"
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml']
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'kotlin-kapt'
apply plugin: 'org.openjfx.javafxplugin'

def arrow_version = "0.10.5"
def kotest_version = "4.2.5"
def koin_version = "2.1.6"
def testfx_version = "4.0.16-alpha"
def testcontainer_version = "1.14.3"

testSets { integrationTest }

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'

    // Tornado
    implementation group: 'no.tornado', name: 'tornadofx', version: "1.7.20"

    // Arrow
    implementation group: 'io.arrow-kt', name: 'arrow-fx', version: "$arrow_version"
    implementation group: 'io.arrow-kt', name: 'arrow-syntax', version: "$arrow_version"
    kapt group: 'io.arrow-kt', name: 'arrow-meta', version: "$arrow_version"

    // Kafka
    compile group: 'org.apache.kafka', name: 'kafka-clients', version: '6.0.0-ce'
    compile group: 'io.confluent', name: 'kafka-schema-registry', version: '5.5.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2'

    // Avro
    compile group: 'io.confluent', name: 'kafka-avro-serializer', version: '5.5.1'

    // Koin
    implementation group: 'org.koin', name: 'koin-core', version: "$koin_version"
    implementation group: 'org.koin', name: 'koin-core-ext', version: "$koin_version"
    testImplementation group: 'org.koin', name: 'koin-test', version: "$koin_version"

    // Json
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-runtime', version: '1.0-M1-1.4.0-rc-218'

    // Kotest
    testImplementation group: 'io.kotest', name: 'kotest-runner-junit5', version: "$kotest_version"
    testImplementation group: 'io.kotest', name: 'kotest-assertions-core', version: "$kotest_version"
    testImplementation group: 'io.kotest', name: 'kotest-property', version: "$kotest_version"
    testImplementation group: 'io.kotest', name: 'kotest-assertions-arrow', version: "$kotest_version"

    testCompile group: 'org.testfx', name: 'testfx-junit5', version: "$testfx_version"

    testImplementation group: 'io.mockk', name: 'mockk', version: '1.10.0'

    integrationTestImplementation group: 'org.testcontainers', name: 'kafka', version: "$testcontainer_version"
}

tasks.withType(Test) { useJUnitPlatform() }

application { mainClassName = 'insulator.AppKt' }

compileKotlin { kotlinOptions.jvmTarget = "1.8" }

compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

jar { manifest { attributes('Main-Class': 'insulator.AppKt') } }

import org.gradle.internal.os.OperatingSystem;

task packageApp(type: Exec) {
    group = 'jpackage'
    dependsOn 'shadowJar'
    def command = ['jpackage',
                   '--input', './build/libs',
                   '--main-jar', 'insulator-all.jar',
                   '-d', '.',
                   '--name', 'Insulator',
                   '--java-options', "'--enable-preview'",
                   '--app-version', "${System.getenv().get("RELEASE_VERSION") ?: "0.0.0"}"]
    if (OperatingSystem.current().isWindows()) {
        command.addAll('--icon', './assets/icon.ico', '--win-dir-chooser', '--win-menu')
    } else if (OperatingSystem.current().isMacOsX()) {
        command.addAll('--icon', './assets/icon.icns')
    } else command.addAll('--icon', './assets/icon.png', '--linux-shortcut')

    commandLine(command)
}

tasks.jacocoTestReport {
    reports { xml.setEnabled(true) }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['insulator/styles/**', 'insulator/views/**'])
        }))
    }
}

project.integrationTest { outputs.upToDateWhen { false } }

check.dependsOn test
check.doLast { tasks.jacocoTestReport }
